
plugins {
    id 'java'
    id 'jacoco'  // Code coverage
    id 'com.github.johnrengelman.shadow' version '8.1.1' // Add Shadow Plugin
}

java {
    sourceCompatibility = JavaVersion.VERSION_22
    targetCompatibility = JavaVersion.VERSION_22
}

jacoco {
    toolVersion = "0.8.12"
}

repositories {
    mavenCentral()
}

dependencies {
    // Add external JARs from the 'lib' directory
    implementation fileTree(dir: 'lib', include: '*.jar')

    // Example: Add additional dependencies from Maven Central (if needed)
    // implementation 'org.apache.commons:commons-lang3:3.12.0'

}

sourceSets {
    // Define the main source set
    main {
        java {
            srcDirs = ['src/main/java', 'src/history/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

tasks.named('compileJava') {
}

// Ensure history classes are included in the JAR
tasks.named('jar') {
    manifest {
        attributes(
            'Implementation-Title': 'javi',
            'Implementation-Version': '1.0',
            'Main-Class': 'javi.Javi', // Replace with your actual main class
            'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' ')
        )
    }
    archiveBaseName.set('javi')
    archiveVersion.set('1.0')
    destinationDirectory.set(file("$buildDir/libs"))
}

// Copy JAR to dist directory (like ant used to do)
tasks.register('dist', Copy) {
    dependsOn jar
    from jar.archiveFile
    into "$projectDir/dist"
    rename { 'javi.jar' }
}

// Copy fat JAR to dist directory
tasks.register('distFat', Copy) {
    dependsOn shadowJar
    from shadowJar.archiveFile
    into "$projectDir/dist"
    rename { 'javi-all.jar' }
}

// Configuration for JaCoCo agent JAR
configurations {
    jacocoAgent
}

dependencies {
    jacocoAgent 'org.jacoco:org.jacoco.agent:0.8.12:runtime'
}

// Run PSTest with JaCoCo coverage
tasks.register('pstestCoverage', JavaExec) {
    dependsOn compileJava
    group = 'verification'
    description = 'Run PSTest with code coverage'
    
    mainClass = 'history.PSTest'
    classpath = sourceSets.main.runtimeClasspath
    
    def execFile = file("${layout.buildDirectory.get()}/jacoco/pstest.exec")
    doFirst {
        execFile.parentFile.mkdirs()
        def agentJar = configurations.jacocoAgent.singleFile
        jvmArgs "-javaagent:${agentJar}=destfile=${execFile}"
    }
}

// Run IntArrayTest with JaCoCo coverage
tasks.register('intArrayTestCoverage', JavaExec) {
    dependsOn compileJava
    group = 'verification'
    description = 'Run IntArrayTest with code coverage'
    
    mainClass = 'history.IntArrayTest'
    classpath = sourceSets.main.runtimeClasspath
    
    def execFile = file("${layout.buildDirectory.get()}/jacoco/intarraytest.exec")
    doFirst {
        execFile.parentFile.mkdirs()
        def agentJar = configurations.jacocoAgent.singleFile
        jvmArgs "-javaagent:${agentJar}=destfile=${execFile}", '-ea'
    }
}

